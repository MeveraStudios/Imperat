import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id("com.vanniktech.maven.publish") version "0.29.0"
}

allprojects {
    group = "dev.velix"
    version = "1.0.0-b.2"

    ext {
        kyori = { String module ->
            kyoriPlatform(module, "4.17.0")
        }

        kyoriPlatform = { String platform, String version ->
            return "net.kyori:adventure-" + platform + ":" + version
        }

        KyoriModule = [
                API          : "api",
                MINI_MESSAGE : "text-minimessage",
                BUKKIT       : "platform-bukkit",
                BUNGEE       : "platform-bungeecord",
                SPONGE       : "platform-spongeapi"
        ]

        lombok = {
            return 'org.projectlombok:lombok:1.18.34'
        }
    }
}

subprojects {
    plugins.apply("java-library")
    plugins.apply("com.vanniktech.maven.publish")

    mavenPublishing {
        coordinates(group as String, "imperat-" + name, version as String)

        pom {
            name = "Imperat"
            description = "A modern customizable command framework."
            inceptionYear = "2024"
            url = "https://github.com/VelixDevelopments/Imperat/"
            licenses {
                license {
                    name = 'MIT'
                    url = "https://opensource.org/licenses/MIT"
                    distribution = "https://mit-license.org/"
                }
            }
            developers {
                developer {
                    id = "mqzn"
                    name = "Mqzn"
                    url = "https://github.com/Mqzn/"
                }
                developer {
                    id = "iiahmedyt"
                    name = "iiAhmedYT"
                    url = "https://github.com/iiAhmedYT/"
                }
            }
            scm {
                url = "https://github.com/VelixDevelopments/Imperat/"
                connection = "scm:git:git://github.com/VelixDevelopments/Imperat.git"
                developerConnection = "scm:git:ssh://git@github.com/VelixDevelopments/Imperat.git"
            }
        }

        publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
        signAllPublications()
    }

}