import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "java-library"
    id "com.vanniktech.maven.publish" version "0.31.0"
}

def releaseSnapshots = false
def isSnapshot = System.getProperty("SNAPSHOT_BUILD") == "true"

allprojects {
    compileJava.options.encoding = "UTF-8"

    group = "dev.velix"
    version = "1.9.0"

    if (isSnapshot && releaseSnapshots) {
        version = version + "-SNAPSHOT"
    }

    ext {
        def kyoriVersion = "4.17.0"
        def kyoriPlatformVersion = "4.3.4"

        kyori = { String module ->
            return "net.kyori:adventure-" + module + ":" + kyoriVersion
        }

        kyoriPlatform = { String module ->
            return "net.kyori:adventure-" + module + ":" + kyoriPlatformVersion
        }

        KyoriModule = [
                API         : "api",
                MINI_MESSAGE: "text-minimessage",
                BUKKIT      : "platform-bukkit",
                BUNGEE      : "platform-bungeecord",
                SPONGE      : "platform-spongeapi"
        ]
    }
}

subprojects {
    plugins.apply("java-library")

    if (project.name == "paper") {
        return
    }

    plugins.apply("com.vanniktech.maven.publish")

    mavenPublishing {
        coordinates(group as String, "imperat-" + name, version as String)

        pom {
            name = "Imperat"
            description = "A modern customizable command framework."
            inceptionYear = "2024"
            url = "https://github.com/VelixDevelopments/Imperat/"
            licenses {
                license {
                    name = 'MIT'
                    url = "https://opensource.org/licenses/MIT"
                    distribution = "https://mit-license.org/"
                }
            }
            developers {
                developer {
                    id = "mqzn"
                    name = "Mqzn"
                    url = "https://github.com/Mqzn/"
                }
                developer {
                    id = "iiahmedyt"
                    name = "iiAhmedYT"
                    url = "https://github.com/iiAhmedYT/"
                }
            }
            scm {
                url = "https://github.com/VelixDevelopments/Imperat/"
                connection = "scm:git:git://github.com/VelixDevelopments/Imperat.git"
                developerConnection = "scm:git:ssh://git@github.com/VelixDevelopments/Imperat.git"
            }
        }

        if (
                !gradle.startParameter.taskNames.any { (it == "publishToMavenLocal") }
                && (!isSnapshot || (isSnapshot && releaseSnapshots))
        ) {
            publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
            signAllPublications()
        }

        compileJava.options.encoding = "UTF-8"
        compileJava.options.compilerArgs += ["-parameters"]
    }

}