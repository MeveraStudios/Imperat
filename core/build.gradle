compileJava.options.encoding = "UTF-8"
compileJava.options.compilerArgs += ["-parameters"]

dependencies {
    compileOnly lombok()
    annotationProcessor lombok()
    implementation kyori(KyoriModule.API)
    implementation kyori(KyoriModule.MINI_MESSAGE)
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'

    compileOnly 'org.slf4j:slf4j-api:2.0.16'

    testImplementation 'org.slf4j:slf4j-api:2.0.16'
    testImplementation 'ch.qos.logback:logback-classic:1.5.7'
}

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
}


static String lombok() {
    return 'org.projectlombok:lombok:1.18.34'
}


static String kyori(KyoriModule module) {
    return platform(module, "4.17.0")
}

static String platform(KyoriModule platform, String version) {
    "net.kyori:adventure-" + platform.getDisplay() + ":" + version
}

enum KyoriModule {

    API("api"), MINI_MESSAGE("text-minimessage"), BUKKIT("platform-bukkit"),
    BUNGEE("platform-bungeecord"), SPONGE("platform-spongeapi");

    private final String display

    KyoriModule(String display) {
        this.display = display
    }

    String getDisplay() {
        return display
    }
}

